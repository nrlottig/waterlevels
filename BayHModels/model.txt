
    model {
    # Likelihood: 
    # Level-1 of the model
    for (i in 1:n){ 
        y[i] ~ dnorm(mu[i], tau)               
        mu[i] <- alpha[group[i]] + beta[group[i]] * precip[i]         
        } 
    # Level-2 of the model
    for(j in 1:J){
        alpha[j] <- BB[j,1]
        beta[j] <- BB[j,2]
        BB[j,1:K] ~ dmnorm(BB.hat[j,], Tau.B[,]) # bivriate normal
        BB.hat[j,1] <- mu.alpha
        BB.hat[j,2] <- mu.beta
        }
    # Priors and derived quantities
    sigma ~ dunif(0, 100)
    tau <- pow(sigma,-2) # precision
    sigma2 <- pow(sigma,2)
    mu.alpha ~ dnorm(0, 0.0001)
    mu.beta ~ dnorm(0, 0.0001)
    
    # Convert covariance matrix to precision for use in bivariate normal above
    Tau.B[1:K,1:K] <- inverse(Sigma.B[,])
    # variance among intercepts
    Sigma.B[1,1] <- pow(sigma.a, 2)
    sigma.a ~ dunif (0, 100)
    
    # Variance among slopes
    Sigma.B[2,2] <- pow(sigma.b, 2)
    sigma.b ~ dunif (0, 100)
    
    # Covariance between alpha's and beta's
    Sigma.B[1,2] <- rho * sigma.a * sigma.b
    Sigma.B[2,1] <- Sigma.B[1,2]
    
    # Uniform prior on correlation
    rho ~ dunif (-1, 1)
    } # end model
    
